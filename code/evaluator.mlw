module Evaluator
  use syntax.Syntax
  use semantics.Semantics
  use int.Int
  use bool.Bool
  use list.List
  use option.Option
  use dict.ImpMap as IM
  use map.Map

  let sigma : IM.state = IM.empty ()

  let rec aeval (a: aexpr id) : int
    variant { a }
    ensures { eval_aexpr a (IM.domain sigma) (Enormal result) }
    raises { IM.Unbound -> eval_aexpr a (IM.domain sigma) (Eabnormal Eunbound) }
  = match a with
    | Acst i -> i
    | Avar v ->  IM.find v sigma
    | Abin a1 op a2 -> (eval_op op) (aeval a1) (aeval a2)
  end

  let rec beval (b: bexpr id) : bool
    variant { b }
    ensures { eval_bexpr b (IM.domain sigma) (Enormal result) }
    raises { IM.Unbound -> eval_bexpr b (IM.domain sigma) (Eabnormal Eunbound) }
  = match b with
    | Bcst b -> b
    | Bleq a1 a2 -> aeval a1 <= aeval a2
    (* Cannot be proven *)
    | Band b1 b2 -> andb (beval b1) (beval b2)
    | Bnot b     -> not (beval b)
  end

  let rec seval (s: stmt id) : ()
     diverges
     ensures {
       eval_stmt s (IM.domain (old sigma)) (Enormal (IM.domain sigma))
     }
     raises {
       IM.Unbound -> eval_stmt s (IM.domain (old sigma)) (Eabnormal Eunbound)
     }
  = match s with
    | Sskip | Sassert _ -> ()
    | Sass x a -> IM.add x (aeval a) sigma
    | Sseq s1 s2 -> seval s1; seval s2
    | Sif b s1 s2 -> if beval b then seval s1 else seval s2
    | Swhile e _ body -> while (beval e) do
                          seval body
                         done
    end

  let eval_prog (s: stmt id) : list (int,int)
    diverges
    ensures {
       eval_stmt s (IM.domain (old sigma)) (Enormal (IM.domain sigma))
    }
    raises { IM.Unbound -> eval_stmt s (IM.domain (old sigma)) (Eabnormal Eunbound) } =
    let _ = seval s in
    let res = IM.state_lst sigma in
    let _ = IM.clear sigma in
    res

    let test ()
      diverges
      raises { IM.Unbound }
    = seval (Sass 1 (Acst 5))

end
